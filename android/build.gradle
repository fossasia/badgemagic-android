plugins {
    id "com.diffplug.spotless" version "5.6.1"
}
apply plugin: "com.github.b3er.local.properties"
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def KEYSTORE_FILE = rootProject.file('scripts/key.jks')
def TRAVIS_BUILD = System.getenv("TRAVIS") == "true" && KEYSTORE_FILE.exists()
def LOCAL_KEY_PRESENT = project.hasProperty('SIGNING_KEY_FILE') && rootProject.file(SIGNING_KEY_FILE).exists()

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId 'org.fossasia.badgemagic'
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 10
        versionName '1.8.0'
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        if (TRAVIS_BUILD) {
            release {
                storeFile KEYSTORE_FILE
                storePassword System.getenv("STORE_PASS")
                keyAlias System.getenv("ALIAS")
                keyPassword System.getenv("KEY_PASS")
            }
        } else if (LOCAL_KEY_PRESENT) {
            release {
                storeFile rootProject.file(SIGNING_KEY_FILE)
                storePassword STORE_PASS
                keyAlias ALIAS
                keyPassword KEY_PASS
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (LOCAL_KEY_PRESENT || TRAVIS_BUILD)
                signingConfig signingConfigs.release
        }
    }

    dataBinding.enabled = true

    packagingOptions {
        pickFirst 'META-INF/kotlinx-serialization-runtime.kotlin_module'
    }
}

spotless {
    kotlin {
        ktlint()
        target '**/*.kt'
    }
}

gradle.projectsEvaluated {
    preBuild.dependsOn(spotlessApply)
}

dependencies {
    // BadgeMagicModule
    implementation project(':BadgeMagicModule')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin_version}"

    kapt "androidx.databinding:compiler:3.2.0-alpha11"

    //Gif Library
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.21'

    //Android X Libraries
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'

    // Koin AndroidX
    implementation "org.koin:koin-androidx-viewmodel:${versions.koin_version}"

    //Material Design
    implementation 'com.google.android.material:material:1.2.0-beta01'

    //Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'no.nordicsemi.android.support.v18:scanner:1.4.3'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
    testImplementation 'org.amshove.kluent:kluent:1.61'

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${versions.leak_canary}"

    //Display third party licenses
    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'
}
